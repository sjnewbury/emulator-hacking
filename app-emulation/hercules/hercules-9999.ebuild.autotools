# Copyright 1999-2016 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2

EAPI="6"

inherit autotools cmake-utils flag-o-matic eutils git-r3

DESCRIPTION="Hercules System/370, ESA/390 and zArchitecture Mainframe Emulator"
HOMEPAGE="http://www.hercules-390.eu/"
#SRC_URI="http://downloads.hercules-390.eu/${P}.tar.gz"
EGIT_REPO_URI=https://github.com/hercules-390/hyperion.git
LICENSE="QPL-1.0"
SLOT="0"
KEYWORDS=""
IUSE="bzip2 custom-cflags +suid"

RDEPEND="bzip2? ( app-arch/bzip2 )
	sys-libs/zlib"
DEPEND="${RDEPEND}"

HERC_BUILD_DIR=${WORKDIR}/${P}_build

src_unpack() {
	git-r3_src_unpack
	git-r3_fetch https://github.com/hercules-390/SoftFloat-3a.git
	git-r3_checkout https://github.com/hercules-390/SoftFloat-3a.git ${WORKDIR}/SoftFloat-3a.git
}

src_prepare() {
	default

	# Can't run eautoreconf since elibtoolize isn't used
	eaclocal
	eautoheader
	eautomake
	eautoconf

	# The local modules need local libs, so when doing a parallel install
	# of the modules and libs breaks during relinking.  Force the libs to
	# install first, and then the modules that use those libs.  #488126
	echo "install-modexecLTLIBRARIES: install-libLTLIBRARIES" >> Makefile.in

	cd "${WORKDIR}"/SoftFloat-3a.git
	cmake-utils_src_prepare
}

src_configure() {
	use custom-cflags || strip-flags

	# HORRIBLE HACK! Build the s3fh library before attempting to
	# configure hercules/hyperion
	cd "${WORKDIR}"/SoftFloat-3a.git
	BUILD_DIR="${WORKDIR}"/s3fh_build cmake-utils_src_configure
	cd "${WORKDIR}"/s3fh_build
	emake
        ln -s "${WORKDIR}"/s3fh_build/extpkg/SoftFloat-3a/install "${WORKDIR}"/s3fh

	mkdir -p "${HERC_BUILD_DIR}"
	cd "${HERC_BUILD_DIR}"
	ac_cv_lib_bz2_BZ2_bzBuffToBuffDecompress=$(usex bzip2) \
	ECONF_SOURCE="${S}" econf \
		$(use_enable bzip2 cckd-bzip2) \
		$(use_enable bzip2 het-bzip2) \
		$(use_enable suid setuid-hercifc) \
		--enable-custom="Gentoo ${PF}.ebuild" \
		--disable-optimization
}

src_compile() {
	cd "${HERC_BUILD_DIR}"
	emake
}

src_install() {
	cd "${HERC_BUILD_DIR}"
	default
	insinto /usr/share/hercules
	cd "${S}"
	doins hercules.cnf
	dodoc README.* RELEASE.NOTES
	dohtml -r html
}
